config {
    type: "incremental",
    schema: project_config.STAGING_DATASET,
    description: "Produces daily total and unique counts of conversions per page",
    bigquery: {
        partitionBy: "event_date_dt",
        clusterBy: ["session_partition_key"],
        labels: {
          table_name: "ga4_pages_conversions_daily"
        }
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(event_date_dt) -${project_config.STATIC_INCREMENTAL_DAYS} from ${self()}`,
      `select cast('${project_config.START_DATE}' as date format 'yyyymmdd')`)
    }
  );

  ${when(incremental(),
    `delete from ${self()} where event_date_dt >= event_date_checkpoint`)
  }
}

with base as (
    select
        session_partition_key
        , event_date_dt
        , page_key
        -- Extract user-defined conversions defined in the config
        -- replace helper with chubb conversions
        ${custom_conversions.chubbConversionsTotalUnique()} 
    from
        ${ref('stg_ga4_events')}
    where event_date_dt >= event_date_checkpoint
    group by 1,2,3
)


select * from base