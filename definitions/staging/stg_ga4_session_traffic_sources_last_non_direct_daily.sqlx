config {
    type: "incremental",
    schema: project_config.STAGING_DATASET,
    description: "Finds the last non-direcct source attributed to each session within a 30-day lookback window. Sessions are split by day.",
    bigquery: {
        partitionBy: "event_date_dt",
        clusterBy: ["client_key", "session_partition_key"],
        labels: {
          table_name: "ga4_sessions_traffic_sources_last_non_direct_daily"
        }
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(event_date_dt) -${project_config.STATIC_INCREMENTAL_DAYS} from ${self()}`,
      `select cast('${project_config.START_DATE}' as date format 'yyyymmdd')`)
    }
  );

  ${when(incremental(),
    `delete from ${self()} where event_date_dt >= event_date_checkpoint`)
  }
}

with last_non_direct_session_partition_key as (
    select
        client_key
        , session_partition_key
        , event_date_dt
        , session_source
        , session_medium
        , session_source_category
        , session_campaign
        , session_content
        , session_term
        , session_default_channel_grouping
        , non_direct_session_partition_key
        , case
            when non_direct_session_partition_key is null
            then 
                last_value(non_direct_session_partition_key ignore nulls) over (partition by client_key order by session_partition_timestamp range between ${helpers.calculateLookbackRange(project_config.SESSION_ATTRIBUTION_LOOKBACK_WINDOW)} preceding and current row)
            else 
                non_direct_session_partition_key
            end as session_partition_key_last_non_direct
    from
        ${ref('stg_ga4_session_traffic_sources_daily')}
    where 
        event_date_dt >= event_date_checkpoint
)
, join_last_non_direct_session_source as (
    select
        last_non_direct_session_partition_key.client_key
        , last_non_direct_session_partition_key.session_partition_key
        , last_non_direct_session_partition_key.event_date_dt
        , last_non_direct_session_partition_key.session_source
        , last_non_direct_session_partition_key.session_medium
        , last_non_direct_session_partition_key.session_source_category
        , last_non_direct_session_partition_key.session_campaign
        , last_non_direct_session_partition_key.session_content
        , last_non_direct_session_partition_key.session_term
        , last_non_direct_session_partition_key.session_default_channel_grouping
        , last_non_direct_session_partition_key.session_partition_key_last_non_direct
        , coalesce(last_non_direct_source.session_source, '(direct)') as last_non_direct_source
        , coalesce(last_non_direct_source.session_medium, '(none)') as last_non_direct_medium
        , coalesce(last_non_direct_source.session_source_category, '(none)') as last_non_direct_source_category
        , coalesce(last_non_direct_source.session_campaign, '(none)') as last_non_direct_campaign
        , coalesce(last_non_direct_source.session_content, '(none)') as last_non_direct_content
        , coalesce(last_non_direct_source.session_term, '(none)') as last_non_direct_term
        , coalesce(last_non_direct_source.session_default_channel_grouping, 'Direct') as last_non_direct_default_channel_grouping
        , ${default_channel_grouping.calculatedTrafficType('last_non_direct_source.session_default_channel_grouping')} as last_non_direct_traffic_type
     from
        last_non_direct_session_partition_key
    left join ${ref('stg_ga4_session_traffic_sources_daily')} last_non_direct_source on
        last_non_direct_session_partition_key.session_partition_key_last_non_direct = last_non_direct_source.session_partition_key
)
, add_traffic_type as (
  select
    client_key
    , session_partition_key
    , event_date_dt
    , session_source
    , session_medium
    , session_source_category
    , session_campaign
    , session_content
    , session_term
    , session_default_channel_grouping 
    , ${default_channel_grouping.calculatedTrafficType('session_default_channel_grouping')} as session_traffic_type
    , last_non_direct_source
    , last_non_direct_medium
    , last_non_direct_source_category
    , last_non_direct_campaign
    , last_non_direct_content
    , last_non_direct_term
    , last_non_direct_default_channel_grouping
    , ${default_channel_grouping.calculatedTrafficType('last_non_direct_default_channel_grouping')} as last_non_direct_traffic_type
  from
    join_last_non_direct_session_source
)

select * from add_traffic_type