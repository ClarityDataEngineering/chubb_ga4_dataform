config {
    type: "incremental",
    disabled: !project_config.INCLUDE_CAMPAIGN_PROPERTIES,
    schema: project_config.STAGING_DATASET,
    description: "Extracts values from UTMs if a naming framework is in place. Will be different per client and therefore will be updated accordingly",
    bigquery: {
        partitionBy: "event_date_dt",
        clusterBy: ["session_partition_key"],
        labels: {
          table_name: "ga4_sessions_campaign_properties"
        }
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(event_date_dt) -${project_config.STATIC_INCREMENTAL_DAYS} from ${self()}`,
      `select cast('${project_config.START_DATE}' as date format 'yyyymmdd')`)
    }
  );

  ${when(incremental(),
    `delete from ${self()} where event_date_dt >= event_date_checkpoint`)
  }
}

with session_utms as (
  select
    event_date_dt
    , session_partition_key
    , last_non_direct_campaign as campaign
    , last_non_direct_content as content
    , last_non_direct_term as term
  from
    ${ref('stg_ga4_session_traffic_sources_last_non_direct_daily')}
  where
    last_non_direct_campaign is not null
    and event_date_dt >= event_date_checkpoint
)
, traffic_params as (
  select
    *
    , CASE 
        -- Version A - October 2025
      WHEN regexp_contains(campaign, r'(?i)^(a)_([a-z0-9]{1})_([a-z0-9]{2})?_([a-z0-9]{2})?_([a-z0-9]{2})_([a-z0-9]{2})_([^_]*)$') THEN
        STRUCT  (
          NULLIF(SPLIT(campaign, "_") [safe_offset(0)], "") as utm_version,
          NULLIF(SPLIT(campaign, "_") [safe_offset(1)], "") as business_line,
          NULLIF(SPLIT(campaign, "_") [safe_offset(2)], "") as industry,
          NULLIF(SPLIT(campaign, "_") [safe_offset(3)], "") as product_service,
          NULLIF(SPLIT(campaign, "_") [safe_offset(4)], "") as location,
          NULLIF(SPLIT(campaign, "_") [safe_offset(5)], "") as language,
          NULLIF(SPLIT(campaign, "_") [safe_offset(6)], "") as campaign,

          -- UTM Content - Free Text Fields
          NULLIF(SPLIT(content, "_") [safe_offset(0)], "") as custom_1,
          NULLIF(SPLIT(content, "_") [safe_offset(1)], "") as custom_2,
          NULLIF(SPLIT(content, "_") [safe_offset(2)], "") as custom_3
        )
      -- Otherwise return nulls
      ELSE 
        STRUCT (
          null as utm_version,
          null as business_line,
          null as industry,
          null as product_service,
          null as location,
          null as language,
          null as campaign,
          null as custom_1,
          null as custom_2,
          null as custom_3
        )
      END as params
  from 
    session_utms
)
, lookup_values as (
  select * from ${ref('stg_util_naming_framework_lookup')}
)
, value_lookup as (
  select
    traffic_params.*
    , ${helpers.campaignPropertyLookupCase(project_config.CAMPAIGN_PROPERTIES_LOOKUP)}
  from 
    traffic_params
    ${helpers.campaignPropertyJoin(project_config.CAMPAIGN_PROPERTIES_LOOKUP, 'lookup_values')}
)
, final as (
  select
    value_lookup.event_date_dt
    , value_lookup.session_partition_key
    , struct (
        value_lookup.params.utm_version
        , business_line_lookup as business_line
        , industry_lookup as industry
        , product_service_lookup as product_service
        , location_lookup as location
        , language_lookup as language
        , campaign_lookup as campaign
        , value_lookup.params.custom_1 as custom_1
        , value_lookup.params.custom_2 as custom_2
        , value_lookup.params.custom_3 as custom_3
    ) as params_renamed
    , value_lookup.params.location as utm_country_shortcode
  from
    value_lookup
  where value_lookup.params.utm_version is not null -- remove null records 
)
select * from final