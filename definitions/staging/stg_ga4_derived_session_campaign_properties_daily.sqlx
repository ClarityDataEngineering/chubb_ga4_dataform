config {
    type: "incremental",
    disabled: !project_config.INCLUDE_CAMPAIGN_PROPERTIES,
    schema: project_config.STAGING_DATASET,
    description: "Extracts values from UTMs if a naming framework is in place. Will be different per client and therefore will be updated accordingly",
    bigquery: {
        partitionBy: "event_date_dt",
        clusterBy: ["session_partition_key"],
        labels: {
          table_name: "ga4_sessions_campaign_properties"
        }
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(event_date_dt) -${project_config.STATIC_INCREMENTAL_DAYS} from ${self()}`,
      `select cast('${project_config.START_DATE}' as date format 'yyyymmdd')`)
    }
  );

  ${when(incremental(),
    `delete from ${self()} where event_date_dt >= event_date_checkpoint`)
  }
}

with session_utms as (
  select
    event_date_dt
    , session_partition_key
    , last_non_direct_campaign as campaign
    , last_non_direct_content as content
    , last_non_direct_term as term
  from
    ${ref('stg_ga4_session_traffic_sources_last_non_direct_daily')}
  where
    last_non_direct_campaign is not null
    and event_date_dt >= event_date_checkpoint
)
, traffic_params as (
  select
    *
    , CASE 
        -- Version c - Jan 2024
      WHEN regexp_contains(campaign, r'(?i)^(c)_([^_]*)_([a-z0-9]{2})_([a-z0-9]{2})_([a-z0-9]{3})_([a-z0-9]{3})_([^_]*)$') THEN
        STRUCT  (
          NULLIF(SPLIT(campaign, "_") [safe_offset(0)], "") as utm_version,
          null as aon_story, 
          NULLIF(SPLIT(campaign, "_") [safe_offset(3)], "") as campaign,
          null as topic,
          NULLIF(SPLIT(campaign, "_") [safe_offset(4)], "") as industry,
          NULLIF(SPLIT(campaign, "_") [safe_offset(5)], "") as solution,
          NULLIF(SPLIT(campaign, "_") [safe_offset(6)], "") as growth_priority,
          NULLIF(SPLIT(campaign, "_") [safe_offset(1)], "") as region,
          NULLIF(SPLIT(campaign, "_") [safe_offset(2)], "") as country,

          -- UTM Content Parse 
          NULLIF(SPLIT(content, "_") [safe_offset(0)], "") as funnel_stage,
          NULLIF(SPLIT(content, "_") [safe_offset(1)], "") as signature_insights,
          null as objective,

          -- Custom Fields
          NULLIF(SPLIT(term, "_") [safe_offset(0)], "") as custom_1,
          NULLIF(SPLIT(term, "_") [safe_offset(1)], "") as custom_2,
          NULLIF(SPLIT(term, "_") [safe_offset(2)], "") as custom_3
        )
        -- Version A - June 2023
      WHEN regexp_contains(campaign, r'(?i)^(a|b)_(\d{1})_([a-z0-9]{2})_([^_]*)_([a-z0-9]{3})_([a-z0-9]{3})_([^_]*)_([^_]*)_([a-z0-9]{2})$') THEN
        STRUCT  (
          NULLIF(SPLIT(campaign, "_") [safe_offset(0)], "") as utm_version,
          NULLIF(SPLIT(campaign, "_") [safe_offset(1)], "") as aon_story,
          NULLIF(SPLIT(campaign, "_") [safe_offset(2)], "") as campaign,
          NULLIF(SPLIT(campaign, "_") [safe_offset(3)], "") as topic,
          NULLIF(SPLIT(campaign, "_") [safe_offset(4)], "") as industry,
          NULLIF(SPLIT(campaign, "_") [safe_offset(5)], "") as solution,
          NULLIF(SPLIT(campaign, "_") [safe_offset(6)], "") as growth_priority,
          NULLIF(SPLIT(campaign, "_") [safe_offset(7)], "") as region,
          NULLIF(SPLIT(campaign, "_") [safe_offset(8)], "") as country,


          -- UTM Content Parse 
          NULLIF(SPLIT(content, "_") [safe_offset(0)], "") as funnel_stage,
          NULLIF(SPLIT(content, "_") [safe_offset(1)], "") as signature_insights,
          NULLIF(SPLIT(content, "_") [safe_offset(2)], "") as objective,

          -- Custom Fields
          NULLIF(SPLIT(term, "_") [safe_offset(0)], "") as custom_1,
          NULLIF(SPLIT(term, "_") [safe_offset(1)], "") as custom_2,
          NULLIF(SPLIT(term, "_") [safe_offset(2)], "") as custom_3
        )
      -- Pre June 2023
      WHEN regexp_contains(campaign, r'(?i)^(\d{1})_([a-z0-9]{2})_([^_]+)_([a-z0-9]{3})_([a-z0-9]{3})_([^_]+)_([a-z0-9]{2})$') THEN
        STRUCT (
          null as utm_version,
          NULLIF(SPLIT(campaign, "_") [safe_offset(0)], "") as aon_story,
          NULLIF(SPLIT(campaign, "_") [safe_offset(1)], "") as campaign,
          NULLIF(SPLIT(campaign, "_") [safe_offset(2)], "") as topic,
          NULLIF(SPLIT(campaign, "_") [safe_offset(3)], "") as industry,
          NULLIF(SPLIT(campaign, "_") [safe_offset(4)], "") as solution,
          null as growth_priority,
          NULLIF(SPLIT(campaign, "_") [safe_offset(5)], "") as region,
          NULLIF(SPLIT(campaign, "_") [safe_offset(6)], "") as country,

          -- UTM Content Parse
          NULLIF(SPLIT(content, "_") [safe_offset(0)], "") as funnel_stage,
          NULLIF(SPLIT(content, "_") [safe_offset(1)], "") as signature_insights,
          NULLIF(SPLIT(content, "_") [safe_offset(2)], "") as objective,

          -- Custom Fields
          NULLIF(SPLIT(term, "_") [safe_offset(0)], "") as custom_1,
          NULLIF(SPLIT(term, "_") [safe_offset(1)], "") as custom_2,
          NULLIF(SPLIT(term, "_") [safe_offset(2)], "") as custom_3
        )
      -- Otherwise return nulls
      ELSE 
        STRUCT (
          null as utm_version,
          null as aon_story,
          null as campaign,
          null as topic,
          null as industry,
          null as solution,
          null as growth_priority,
          null as region,
          null as country,
          null as funnel_stage,
          null as signature_insights,
          null as objective,
          null as custom_1,
          null as custom_2,
          null as custom_3
        )
      END as params
  from 
    session_utms
)
, lookup_values as (
  select * from ${ref(project_config.CAMPAIGN_PROPERTIES_LOOKUP_TABLE)}
)
, value_lookup as (
  select
    traffic_params.*
    , ${helpers.campaignPropertyLookupCase(project_config.CAMPAIGN_PROPERTIES_LOOKUP)}
  from 
    traffic_params
    ${helpers.campaignPropertyJoin(project_config.CAMPAIGN_PROPERTIES_LOOKUP, 'lookup_values')}
)
, final as (
  select
    value_lookup.event_date_dt
    , value_lookup.session_partition_key
    , struct (
        value_lookup.params.utm_version
        , aon_story_lookup as aon_story
        , campaign_lookup as campaign
        , topic_lookup as topic
        , industry_lookup as industry
        , solution_lookup as solution
        , growth_priority_lookup as growth_priority
        , value_lookup.params.region as region
        , country_lookup as country
        -- Account for older framework not using shortcodes. Pass through any value
        , COALESCE(funnel_stage_lookup, value_lookup.params.funnel_stage) as funnel_stage
        , COALESCE(signature_insights_lookup, REGEXP_REPLACE(value_lookup.params.signature_insights, r'-', ' ')) as signature_insights
        , COALESCE(objective_lookup, REGEXP_REPLACE(value_lookup.params.objective, r'-', ' ')) as objective
        , value_lookup.params.custom_1 as custom_1
        , value_lookup.params.custom_2 as custom_2
        , value_lookup.params.custom_3 as custom_3
    ) as params_renamed
    , value_lookup.params.country as utm_country_shortcode
  from
    value_lookup
  where value_lookup.params.utm_version is not null -- remove null records 
)
select * from final