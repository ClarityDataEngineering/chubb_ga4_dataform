config {
    type: "incremental",
    schema: project_config.STAGING_DATASET,
    description: "Produces page dimensions, split by day",
    bigquery: {
        partitionBy: "event_date_dt",
        clusterBy: ["client_key, session_partition_key"],
        labels: {
          table_name: "ga4_pages_daily"
        }
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(event_date_dt) -${project_config.STATIC_INCREMENTAL_DAYS} from ${self()}`,
      `select cast('${project_config.START_DATE}' as date format 'yyyymmdd')`)
    }
  );

  ${when(incremental(),
    `delete from ${self()} where event_date_dt >= event_date_checkpoint`)
  }
}

with pageviews_base as (
    select
        client_key
        , session_key
        , session_partition_key -- Leave session keys in so we can enrich and re-agg in final marts
        , event_date_dt
        , event_timestamp
        , event_key
        , page_path
        , page_location
        , page_location_clean
        , page_title
        , page_hostname
        , page_query_string
        , page_referrer
        , original_page_location
        , page_key
        , to_base64(md5(concat(session_key, original_page_location))) as page_engagement_key
        ${project_config.PAGE_VIEW_DERIVED_EVENT_PROPERTIES.length ? 
            `
              ${helpers.unnestUserDefinedProperties(false, project_config.PAGE_VIEW_DERIVED_EVENT_PROPERTIES)}

            ` 
            : ''}
        , LAG(page_location, 1) OVER (PARTITION BY session_partition_key ORDER BY event_timestamp ASC) AS previous_page
        , LAG(page_location, 2) OVER (PARTITION BY session_partition_key ORDER BY event_timestamp ASC) AS previous_page_2
        , LAG(page_location, 3) OVER (PARTITION BY session_partition_key ORDER BY event_timestamp ASC) AS previous_page_3
        , LEAD(page_location, 1) OVER (PARTITION BY session_partition_key ORDER BY event_timestamp ASC) AS next_page
        , LEAD(page_location, 2) OVER (PARTITION BY session_partition_key ORDER BY event_timestamp ASC) AS next_page_2
        , PERCENT_RANK() OVER (PARTITION BY session_partition_key ORDER BY event_timestamp ASC) AS page_view_percent_in_journey
        , CASE WHEN 
            FIRST_VALUE(event_key) OVER (PARTITION BY session_partition_key ORDER BY event_timestamp DESC) = event_key 
            THEN 1 
            ELSE 0 
          END AS is_exit
        , is_entrance
        , is_page_view
        , engagement_time_msec
    from
        ${ref('stg_ga4_events')}
    where event_name = 'page_view'
    and event_date_dt >= event_date_checkpoint
)

select * from pageviews_base