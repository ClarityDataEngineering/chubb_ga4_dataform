config {
    type: "incremental",
    schema: project_config.OUTPUT_DATASET,
    description: "CTA Click event table enriched with session params.",
    bigquery: {
        partitionBy: "event_date_dt",
        clusterBy: ["page_location"],
        labels: {
          table_name: "ga4_final_event_cta_click"
        }
    }
}


pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(event_date_dt) -${project_config.STATIC_INCREMENTAL_DAYS} from ${self()}`,
      `select cast('${project_config.START_DATE}' as date format 'yyyymmdd')`)
    }
  );

  ${when(incremental(),
    `delete from ${self()} where event_date_dt >= event_date_checkpoint`)
  }
}

with click_events as (
    select
        client_key
        , session_key
        , session_partition_key
        , event_date_dt
        , page_path
        , page_location
        , page_location_clean
        , page_hostname
        , page_query_string
        , page_key
        ${project_config.CLICK_DERIVED_EVENT_PROPERTIES.length ? 
            `
                ${helpers.userDefinedPropertiesOutput(false, project_config.CLICK_DERIVED_EVENT_PROPERTIES)}
            ` 
            : ''}
        , countif(event_name = 'click') as cta_click_total
        , count(distinct if(event_name = 'click', event_name, null)) as cta_click_unique
    from
        ${ref('stg_ga4_event_cta_click')}
    where
        event_date_dt >= event_date_checkpoint
    group by
        all
)
, session_attribution as (
    select
        session_partition_key
        , event_date_dt
        , last_non_direct_source
        , last_non_direct_medium
        , last_non_direct_source_category
        , last_non_direct_campaign
        , last_non_direct_content
        , last_non_direct_term
        , last_non_direct_default_channel_grouping
        , last_non_direct_traffic_type
    from
        ${ref('stg_ga4_session_traffic_sources_last_non_direct_daily')}
    where
        event_date_dt >= event_date_checkpoint
)
, session_campaign_properties as (
    select
        *
    from 
        ${ref('stg_ga4_derived_session_campaign_properties_daily')}
    where
        event_date_dt >= event_date_checkpoint
)
, chubb_session_calc_geo as (
    select
        *
    from
        ${ref('stg_ga4_session_calculated_geo_daily')}
    where
        event_date_dt >= event_date_checkpoint
)
, reagg_by_day as (
    select
        se.event_date_dt
        , se.page_path
        , se.page_location
        , se.page_location_clean
        , se.page_hostname
        , se.page_query_string
        , se.page_key
        ${project_config.CLICK_DERIVED_EVENT_PROPERTIES.length ? 
                ` 
                    ${helpers.userDefinedPropertiesOutput(false, project_config.CLICK_DERIVED_EVENT_PROPERTIES)}
                
                ` 
                : ''}
        , sa.last_non_direct_source
        , sa.last_non_direct_medium
        , sa.last_non_direct_source_category
        , sa.last_non_direct_campaign
        , sa.last_non_direct_content
        , sa.last_non_direct_term
        , sa.last_non_direct_default_channel_grouping
        , sa.last_non_direct_traffic_type
        , scp.params_renamed.utm_version
        , scp.params_renamed.business_line
        , scp.params_renamed.industry
        , scp.params_renamed.product_service
        , scp.params_renamed.location
        , scp.params_renamed.language
        , scp.params_renamed.campaign
        , scp.params_renamed.objective
        , scp.params_renamed.custom_1
        , scp.params_renamed.custom_2
        , scp.params_renamed.custom_3
        , cg.session_calculated_geo.region as calc_region
        , cg.session_calculated_geo.location as calc_location
        , sum(se.cta_click_total) cta_click_total  --reagg totals
        , sum(se.cta_click_unique) cta_click_unique

        from
            click_events se
        left join session_attribution sa
            on se.session_partition_key = sa.session_partition_key
            and se.event_date_dt = sa.event_date_dt
        left join session_campaign_properties scp
            on se.session_partition_key = scp.session_partition_key
        left join chubb_session_calc_geo cg
            on se.session_partition_key = cg.session_partition_key
            and se.event_date_dt = cg.event_date_dt
        group by all
)


select
    event_date_dt
    , page_path
    , page_location
    , page_location_clean
    , page_hostname
    , page_query_string
    , page_key
    , ${url_parsing.siteLocationByURL('page_location_clean')}
    ${project_config.CLICK_DERIVED_EVENT_PROPERTIES.length ? 
            ` , struct (
                ${helpers.userDefinedPropertiesOutput(true, project_config.CLICK_DERIVED_EVENT_PROPERTIES)}
            ) as custom_event_properties
             
            ` 
            : ''}
    , struct (
        last_non_direct_source
        , last_non_direct_medium
        , last_non_direct_source_category
        , last_non_direct_campaign
        , last_non_direct_content
        , last_non_direct_term
        , last_non_direct_default_channel_grouping
        , last_non_direct_traffic_type
    ) as session_attribution
    , struct (
        utm_version
        , business_line
        , industry
        , product_service
        , location
        , language
        , campaign
        , objective
        , custom_1
        , custom_2
        , custom_3
    ) as session_campaign_properties
    , struct (
        calc_region
        , calc_location
    ) as session_calculated_geo
    , cta_click_total
    , cta_click_unique

from
    reagg_by_day 
