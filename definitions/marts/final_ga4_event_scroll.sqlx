config {
    type: "incremental",
    schema: project_config.OUTPUT_DATASET,
    description: "Scroll event table enriched with session params.",
    bigquery: {
        partitionBy: "event_date_dt",
        clusterBy: ["page_location"],
        labels: {
          table_name: "ga4_final_event_scroll"
        }
    }
}


pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(event_date_dt) -${project_config.STATIC_INCREMENTAL_DAYS} from ${self()}`,
      `select cast('${project_config.START_DATE}' as date format 'yyyymmdd')`)
    }
  );

  ${when(incremental(),
    `delete from ${self()} where event_date_dt >= event_date_checkpoint`)
  }
}

with scroll_events as (
    select
        client_key
        , session_key
        , session_partition_key
        , event_date_dt
        , page_path
        , page_location
        , page_location_clean
        , page_hostname
        , page_query_string
        , page_key
        ${project_config.SCROLL_DERIVED_EVENT_PROPERTIES.length ? 
            `
                ${helpers.userDefinedPropertiesOutput(false, project_config.SCROLL_DERIVED_EVENT_PROPERTIES)}
            ` 
            : ''}
        , countif(event_name = 'scroll') as scroll_total
        , count(distinct if(event_name = 'scroll', event_name, null)) as scroll_unique
    from
        ${ref('stg_ga4_event_scroll')}
    where
        event_date_dt >= event_date_checkpoint
    group by
        all
)
, session_attribution as (
    select
        session_partition_key
        , event_date_dt
        , last_non_direct_source
        , last_non_direct_medium
        , last_non_direct_source_category
        , last_non_direct_campaign
        , last_non_direct_content
        , last_non_direct_term
        , last_non_direct_default_channel_grouping
        , last_non_direct_traffic_type
    from
        ${ref('stg_ga4_session_traffic_sources_last_non_direct_daily')}
    where
        event_date_dt >= event_date_checkpoint
)
, reagg_by_day as (
    select
        se.event_date_dt
        , se.page_path
        , se.page_location
        , se.page_location_clean
        , se.page_hostname
        , se.page_query_string
        , se.page_key
        ${project_config.SCROLL_DERIVED_EVENT_PROPERTIES.length ? 
                ` 
                    ${helpers.userDefinedPropertiesOutput(false, project_config.SCROLL_DERIVED_EVENT_PROPERTIES)}
                
                ` 
                : ''}
        , sa.last_non_direct_source
        , sa.last_non_direct_medium
        , sa.last_non_direct_source_category
        , sa.last_non_direct_campaign
        , sa.last_non_direct_content
        , sa.last_non_direct_term
        , sa.last_non_direct_default_channel_grouping
        , sa.last_non_direct_traffic_type
        , sum(se.scroll_total) scroll_total  --reagg totals
        , sum(se.scroll_unique) scroll_unique

        from
            scroll_events se
        left join session_attribution sa
            on se.session_partition_key = sa.session_partition_key
            and se.event_date_dt = sa.event_date_dt
        group by all
)


select
    event_date_dt
    , page_path
    , page_location
    , page_location_clean
    , page_hostname
    , page_query_string
    , page_key
    ${project_config.SCROLL_DERIVED_EVENT_PROPERTIES.length ? 
            ` , struct (
                ${helpers.userDefinedPropertiesOutput(true, project_config.SCROLL_DERIVED_EVENT_PROPERTIES)}
            ) as custom_event_properties
             
            ` 
            : ''}
    , struct (
        last_non_direct_source
        , last_non_direct_medium
        , last_non_direct_source_category
        , last_non_direct_campaign
        , last_non_direct_content
        , last_non_direct_term
        , last_non_direct_default_channel_grouping
        , last_non_direct_traffic_type
    ) as session_attribution
    , scroll_total
    , scroll_unique

from
    reagg_by_day 
